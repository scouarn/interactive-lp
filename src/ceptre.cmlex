name LexFn
alphabet 129

set digit = (range '0 '9)
set posdigit = (range '1 '9)
set id_start = (| '\ '/ '- (range 'a 'z 'A 'Z))
set id_cont = (| id_start digit '_ '')
set white = (| 32 9 10 13)  /* space, tab, lf, cr */
set notnewln = (~ 10)

regexp ident = (seq id_start (* id_cont))
regexp ws = (+ white)
regexp number = (| '0 (seq posdigit (* digit)))

set nonesc = (~ (| '\ '"))
regexp esc = (seq '\ notnewln)
regexp string = (seq '" (* (| nonesc esc)) '")

function lexmain: t = 
   eos => eof 
   "pred" => pred
   "stage" => stage
   "context" => context
   ws => skip
   (seq '# (+ id_start)) => hashident
   (seq '% (* notnewln)) => skip
   (seq '= '=) => unify
   (seq '< '>) => differ
   '{ => lbrace
   '} => rbrace
   '( => lparen
   ') => rparen
   '. => period
   ': => colon
   ', => comma
   '= => equals
   '_ => uscore
   '! => bang
   '$ => dollar
   '* => star
   "<-" => larrow
   "->" => rarrow
   "o-" => llolli
   "-o" => rlolli
   number => num
   string => str
   ident => ident
   any => error
   epsilon => error
